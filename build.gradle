buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "one.leftshift.asteria:asteria-dependency:latest.release"
        classpath "one.leftshift.asteria:asteria-email:latest.release"
        classpath "one.leftshift.asteria:asteria-publish:latest.release"
        classpath "one.leftshift.asteria:asteria-version:latest.release"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
    }
}

apply plugin: "java"
apply plugin: "one.leftshift.asteria-email"
apply plugin: "one.leftshift.asteria-publish"
apply plugin: "com.jfrog.bintray"

asteriaEmail {
    smtpHost = System.getenv("ASTERIA_EMAIL_SMTP_HOST")
    smptUser = System.getenv("ASTERIA_EMAIL_SMTP_USER")
    smptPassword = System.getenv("ASTERIA_EMAIL_SMTP_PASSWORD")
    sender = System.getenv("ASTERIA_EMAIL_SENDER")
}

asteriaPublish {
    enableBranchSnapshotRepositories = true
}

allprojects {
    apply plugin: "one.leftshift.asteria-dependency"
    apply plugin: "one.leftshift.asteria-version"
    apply plugin: "java"
    apply plugin: "kotlin"
    apply plugin: 'kotlin-kapt'
    apply plugin: "idea"

    repositories {
        jcenter()
    }

    asteriaDependency {
        enableBranchSnapshotRepositories = true
    }

    dependencies {
        testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        testImplementation "org.jetbrains.kotlin:kotlin-reflect"

        testImplementation "org.assertj:assertj-core"
        testImplementation "org.junit.jupiter:junit-jupiter-engine"
        testImplementation "io.reactivex.rxjava2:rxjava"
        testImplementation "one.leftshift.dynabuffers:dynabuffers"
        testImplementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.9"
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    configurations { provided }
    idea {
        module {
            scopes.COMPILE.plus << configurations.provided
        }
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }
        test {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }
    }

    idea {
        module {
            inheritOutputDirs false
            outputDir compileKotlin.destinationDir
            testOutputDir compileTestKotlin.destinationDir
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }

    test {
        useJUnitPlatform {
        }
    }

    tasks.withType(Test) { testTask ->
        minHeapSize = "1G"
        maxHeapSize = "2G"
        testTask.beforeTest { descriptor ->
            logger.lifecycle(descriptor.toString())
        }
        testTask.afterTest { descriptor, result ->
            println "\n$descriptor.className [$descriptor.classDisplayName] > $descriptor.name [$descriptor.displayName]: $result.resultType"
        }
    }


    if (System.getenv("CI")) {
        afterEvaluate { rootProject.tasks.postRelease.finalizedBy rootProject.tasks.sendReleaseEmail }
        if (tasks.findByName("publish")) {
            afterEvaluate { rootProject.tasks.postRelease.dependsOn tasks.publish }
        }
    }
}

repositories {
    jcenter()
    maven {
        credentials(AwsCredentials) {
            accessKey gradle.awsAccessKey
            secretKey gradle.awsSecretKey
        }
        url gradle.releasesRepositoryUrl
    }
    maven {
        credentials(AwsCredentials) {
            accessKey gradle.awsAccessKey
            secretKey gradle.awsSecretKey
        }
        url gradle.snapshotsRepositoryUrl
    }
}

sourceSets {
    main {
        resources {
            srcDirs "leftshiftone"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name
            from components.java
            pom.withXml {
                Node root = asNode()
                root.appendNode("name", "SKILL CONTRACTS")
                root.appendNode("description", "SKILL CONTRACTS is an open source library that contains multiple contracts compatible with GAIA Skills.")
                root.appendNode("url", "https://github.com/leftshiftone/skill-contracts")

                Node licenses = root.appendNode("licenses")
                Node license = licenses.appendNode("license")
                license.appendNode("name", "Apache License 2.0")
                license.appendNode("url", "https://github.com/leftshiftone/skill-contracts/blob/master/LICENSE")

                Node developers = root.appendNode("developers")
                Node developer = developers.appendNode("developer")
                developer.appendNode("id", "leftshiftone")
                developer.appendNode("name", "Leftshift One")
                developer.appendNode("email", "contact@leftshift.one")
                developer.appendNode("organization", "Leftshift One Software GmbH")
                developer.appendNode("organizationUrl", "https://www.leftshift.one")

                Node scm = root.appendNode("scm")
                scm.appendNode("connection", "scm:git:git://github.com/leftshiftone/skill-contracts.git")
                scm.appendNode("developerConnection", "scm:git:ssh://github.com:leftshiftone/skill-contracts.git")
                scm.appendNode("url", "https://github.com/leftshiftone/skill-contracts")
            }
        }
    }
}

bintray {
    logger.info "Bintray deploy with user [${System.getenv('BINTRAY_USER')}] while working on ${project.name}"
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    override = true
    pkg {
        repo = "skill-contracts"
        name = "${project.group}.${project.name}"
        userOrg = "leftshiftone"
        licenses = ["Apache License 2.0"]
        vcsUrl = "https://github.com/leftshiftone/skill-contracts.git"
        publications = ["mavenJava"]
    }
}

if (System.getenv("CI")) {
    publish.finalizedBy bintrayUpload
}

if (System.getenv("CI") == null || System.getenv("CI") != "true") {
    tasks.release.dependsOn tasks.generateLock
    tasks.release.dependsOn tasks.saveLock
    tasks.release.dependsOn tasks.persistDependencyLock
}
